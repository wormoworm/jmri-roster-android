apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def versionMajor = project.property('versionMajor').toInteger()
def versionMinor = project.property('versionMinor').toInteger()
def versionPatch = project.property('versionPatch').toInteger()

def getVersionCode = { ->
    return versionMajor * 100000000 + versionMinor * 1000000 + versionPatch
}

def getVersionName = { ->
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "uk.tomhomewood.jmriroster"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode getVersionCode()
        versionName getVersionName()

        buildConfigField "String", "ROSTER_API_URL", "\"https://roster.tomstrains.co.uk/api/\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystorepath)
            storePassword keystorepassword
            keyAlias "jmri-roster"
            keyPassword keypasswordjmriroster
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation ('com.github.thekhaeng:recycler-margin:1.2.1') {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    implementation project(":lib")

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}